
grammar Hummingbird
  root           <- statements:statement_list <Root>

  block          <- "{" allspace statement_list "}"

  statement_list <- head:statement tail:(terminator statement)* allspace
  statement      <- (assignment / expr / control)

  assignment <- lvalue space "=" space rvalue <Assignment>
  lvalue     <- let / var / path
  rvalue     <- expr
  path       <- name (indexer / property)*

  expr       <- (literal !(_ binaryop) / binary / unary / chain / group) <Expression>
  lexpr      <- literal / unary / chain / group

  control    <- if / while / try
  if         <- "if" _ expr _ block
  while      <- "while" _ expr _ block
  try        <- "try" _ block _ "catch" space name _ block

  let        <- declarator:"let" space name typifier:(":" space type)?
  var        <- declarator:"var" space name typifier:(":" space type)?

  name       <- [A-Za-z] [A-Za-z0-9_]*
  type       <- [A-Z] [A-Za-z0-9_]*

  chain      <- name (indexer / property / arguments)*
  binary     <- lexpr _ binaryop _ expr <Binary>
  unary      <- unaryoperator expr
  group      <- "(" _ expr _ ")" <Group>

  indexer    <- "[" expr "]"
  property   <- "." name
  arguments  <- "(" _ (expr _ ("," _ expr _)* )? _ ")"

  terminator <- _ "\n" allspace <Skip>
  space      <- [ \t]+
  _          <- space?
  allspace   <- [ \n\r\t]* <Skip>

  literal <- integer <Literal>
  integer <- ("0" / ("-"? [1-9] [0-9]*)) <Integer>

  binaryop       <- add / sub / mul / div / mod
  unaryoperator  <- not
  add            <- "+"
  sub            <- "-"
  mul            <- "*"
  div            <- "/"
  mod            <- "%"
  not            <- "!"
